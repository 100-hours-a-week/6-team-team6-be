plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.1' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
    id 'checkstyle'
    id 'org.sonarqube' version '7.2.2.6593'
}

group = 'backend'
version = '0.0.1-SNAPSHOT'
description = 'billage'

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(25)
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation platform('org.springframework.boot:spring-boot-dependencies:4.0.1')

        compileOnly 'org.projectlombok:lombok'

        annotationProcessor platform('org.springframework.boot:spring-boot-dependencies:4.0.1')
        annotationProcessor 'org.projectlombok:lombok'

        testCompileOnly 'org.projectlombok:lombok'

        testAnnotationProcessor platform('org.springframework.boot:spring-boot-dependencies:4.0.1')
        testAnnotationProcessor 'org.projectlombok:lombok'

    }

    if (project.name != 'ktb-billage-global') {
        dependencies {
            implementation 'org.springframework:spring-context'
        }
    }

    test {
        useJUnitPlatform()
    }
}

checkstyle {
    toolVersion = "13.0.0"
    configFile = file("config/checkstyle/naver-checkstyle-rules.xml")
}


sourceSets {
    integrationTest {
        java {
            srcDir 'src/integrationTest/java'
        }
        resources {
            srcDir 'src/integrationTest/resources'
        }
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    useJUnitPlatform()
}
